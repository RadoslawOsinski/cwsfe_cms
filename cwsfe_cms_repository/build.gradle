import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

apply plugin: 'maven'
apply plugin: 'java-library'
apply plugin: 'base'
apply plugin: 'jacoco'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'org.junit.platform.gradle.plugin'

version = project.ext.project_version
group = "eu.com.cwsfe.cms"
ext.moduleName = 'eu.com.cwsfe.cms.db'
compileJava.options.encoding = 'UTF-8'

configurations.all {
    exclude group: 'org.jboss.spec.javax.transaction', module: 'jboss-transaction-api_1.2_spec'
}

configurations {
    tests
}

dependencies {
    implementation group: 'com.zaxxer', name: 'HikariCP', version: project.ext.hikari_version
    implementation group: 'org.springframework', name: 'spring-tx', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-jdbc', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-core', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-beans', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-context', version: project.ext.spring_version
    api group: 'org.springframework', name: 'spring-context-support', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-web', version: project.ext.spring_version  //dla CommonsMultipartFile
    implementation group: 'org.springframework', name: 'spring-orm', version: project.ext.spring_version
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: project.ext.logback_version
    implementation 'org.apache.commons:commons-lang3:' + project.ext.commons_lang3_version

    implementation group: 'com.hazelcast', name: 'hazelcast', version: project.ext.hazelcast_version
    implementation group: 'com.hazelcast', name: 'hazelcast-spring', version: project.ext.hazelcast_version
    implementation group: 'com.hazelcast', name: 'hazelcast-hibernate52', version: project.ext.hazelcast_hibernate52_version

    api 'org.hibernate:hibernate-validator:' + project.ext.hibernate_validator_version
    api 'org.hibernate:hibernate-core:' +project.ext.hibernate_core_version
    api 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:' + project.ext.hibernate_jpa2_api_version

    api group: 'org.slf4j', name: 'slf4j-api', version: project.ext.slf4j_version
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: project.ext.logback_version
    implementation group: 'org.postgresql', name: 'postgresql', version: project.ext.postgresql_version
    implementation group: 'org.flywaydb', name: 'flyway-core', version: project.ext.flyway_core_version
    implementation group: 'commons-io', name: 'commons-io', version: project.ext.commons_io_version
    testCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: project.ext.junit5_platform_version

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.ext.junit5_version
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.ext.junit5_version
    testCompile group: 'org.junit.platform', name: 'junit-platform-launcher', version: project.ext.junit5_platform_version

    testCompile group: 'org.springframework', name: 'spring-test', version: project.ext.spring_version
    testRuntime group: 'org.springframework', name: 'spring-test', version: project.ext.spring_version
}

def gitCommitHash = "git rev-parse HEAD".execute().text.trim();

compileJava {
    inputs.property("moduleName", moduleName)
    options.compilerArgs += [
        "--module-path", classpath.asPath
    ]
}

jar.archiveName = "CWSFE_CMS_REPOSITORY-" + version + ".jar"
jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes(
            'Automatic-Module-Name': moduleName,
            'Specification-Title': project.name,
            'Specification-Version': version,
            'build-number': System.getenv().BUILD_NUMBER == null ? 'local build' : System.getenv().BUILD_NUMBER,
            'build-id': System.getenv().BUILD_ID == null ? 'local build' : System.getenv().BUILD_ID,
            'Build-Time': ZonedDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd'T'HH:mm:ssZ")),
            'Build-Url': System.getenv().BUILD_URL == null ? 'local build' : System.getenv().BUILD_URL,
            'scm-version': gitCommitHash == null ? 'local build' : gitCommitHash,
            'Build-Tag': System.getenv().BUILD_TAG == null ? 'local build' : System.getenv().BUILD_TAG,
            'Jenkins-Url': System.getenv().JENKINS_URL == null ? 'local build' : System.getenv().JENKINS_URL,
            'Build-Workspace': System.getenv().WORKSPACE == null ? 'local build' : System.getenv().WORKSPACE,
            'Git-Url': System.getenv().GIT_URL == null ? 'local build' : System.getenv().GIT_URL,
            'Git-Branch': System.getenv().GIT_BRANCH == null ? 'local build' : System.getenv().GIT_BRANCH
        )
    }
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "TEST-${project.archivesBaseName}".toUpperCase()
    from sourceSets.test.output
}

task integration_tests_local(type: Test) {
    systemProperty "spring.profiles.test-groups", "integration-tests-local"
    systemProperty "spring.profiles.active", "tomcat";
    systemProperty "test-groups", "integration-tests-local";
    systemProperty "test-groups", "integration-tests-local";

    //pass environment variable parameters from .gradle/gradle.properties to environment variable parameters in test task
    systemProperty "cwsfe_cms.jdbc.driverClassName", System.properties['cwsfe_cms.jdbc.driverClassName'];
    systemProperty "cwsfe_cms.jdbc.url", System.properties['cwsfe_cms.jdbc.url'];
    systemProperty "cwsfe_cms.jdbc.user", System.properties['cwsfe_cms.jdbc.user'];
    systemProperty "cwsfe_cms.jdbc.password", System.properties['cwsfe_cms.jdbc.password'];
}

artifacts {
    tests testJar
}

jacoco {
    toolVersion = project.ext.jacoco_version
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
        xml.enabled true
        csv.enabled false
        html.setDestination(new File("${buildDir}/reports/coverage"))
    }
    executionData = files('build/jacoco/test.exec', 'build/jacoco/integration_tests_local.exec')
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'cwsfe_cms_repository'
        property 'sonar.projectName', 'CWSFE eu.com.cwsfe.cms repository'
        property 'sonar.sources', 'src/main'
        property 'sonar.tests', 'src/test'
        property 'sonar.sourceEncoding', 'UTF-8'
    }
}
