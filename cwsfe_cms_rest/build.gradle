import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

apply plugin: 'maven'
apply plugin: 'java-library'
apply plugin: 'base'
apply plugin: 'jacoco'
apply plugin: 'org.junit.platform.gradle.plugin'

version = project.ext.project_version
group = "eu.com.cwsfe.cms"
ext.moduleName = 'eu.com.cwsfe.cms.rest'
compileJava.options.encoding = 'UTF-8'

dependencies {
    api project(':cwsfe_cms_services')
    implementation group: 'org.springframework', name: 'spring-webmvc', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-tx', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-core', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-beans', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-context', version: project.ext.spring_version
    implementation group: 'org.springframework', name: 'spring-web', version: project.ext.spring_version  //dla CommonsMultipartFile
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: project.ext.javax_annotation_api_version
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: project.ext.jackson_version
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: project.ext.jackson_version
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project.ext.jackson_version
    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: project.ext.old_servlet_api_version
//    testImplementation group: 'javax.servlet', name: 'javax.servlet-api', version: project.ext.servlet_api_version //3.1.0 -newer version has problems with tests
    testImplementation group: 'javax.servlet', name: 'servlet-api', version: project.ext.old_servlet_api_version //3.1.0 -newer version has problems with tests
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.ext.junit5_version
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: project.ext.junit5_platform_version
    testImplementation group: 'org.springframework', name: 'spring-test', version: project.ext.spring_version
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path', version: project.ext.json_path_version
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path-assert', version: project.ext.json_path_version
    testImplementation group: 'org.mockito', name: 'mockito-core', version: project.ext.mockito_version
}

def gitCommitHash = "git rev-parse HEAD".execute().text.trim();

compileJava {
    inputs.property("moduleName", moduleName)
    options.compilerArgs += [
        "--module-path", classpath.asPath
    ]
}

jar.archiveName = "CWSFE_CMS_REST-" + version + ".jar";
jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes(
            'Automatic-Module-Name': moduleName,
            'Specification-Title': project.name,
            'Specification-Version': version,
            'build-number': System.getenv().BUILD_NUMBER == null ? 'local build' : System.getenv().BUILD_NUMBER,
            'build-id': System.getenv().BUILD_ID == null ? 'local build' : System.getenv().BUILD_ID,
            'Build-Time': ZonedDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd'T'HH:mm:ssZ")),
            'Build-Url': System.getenv().BUILD_URL == null ? 'local build' : System.getenv().BUILD_URL,
            'scm-version': gitCommitHash == null ? 'local build' : gitCommitHash,
            'Build-Tag': System.getenv().BUILD_TAG == null ? 'local build' : System.getenv().BUILD_TAG,
            'Jenkins-Url': System.getenv().JENKINS_URL == null ? 'local build' : System.getenv().JENKINS_URL,
            'Build-Workspace': System.getenv().WORKSPACE == null ? 'local build' : System.getenv().WORKSPACE,
            'Git-Url': System.getenv().GIT_URL == null ? 'local build' : System.getenv().GIT_URL,
            'Git-Branch': System.getenv().GIT_BRANCH == null ? 'local build' : System.getenv().GIT_BRANCH
        )
    }
}

task integration_tests_local(type: Test) {
    systemProperty "spring.profiles.test-groups", "integration-tests-local"
    systemProperty "spring.profiles.active", "tomcat";
    systemProperty "test-groups", "integration-tests-local";
    systemProperty "test-groups", "integration-tests-local";

    //pass environment variable parameters from .gradle/gradle.properties to environment variable parameters in test task
    systemProperty "cwsfe_cms.jdbc.driverClassName", System.properties['cwsfe_cms.jdbc.driverClassName'];
    systemProperty "cwsfe_cms.jdbc.url", System.properties['cwsfe_cms.jdbc.url'];
    systemProperty "cwsfe_cms.jdbc.user", System.properties['cwsfe_cms.jdbc.user'];
    systemProperty "cwsfe_cms.jdbc.password", System.properties['cwsfe_cms.jdbc.password'];
}

jacoco {
    toolVersion = project.ext.jacoco_version
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
        xml.enabled true
        csv.enabled false
        html.setDestination(new File ("${buildDir}/reports/coverage"))
    }
    executionData = files('build/jacoco/test.exec', 'build/jacoco/integration_tests_local.exec')
}

sonarqube {
    properties {
        property "sonar.projectKey", 'cwsfe_cms_rest'
        property "sonar.projectName", 'CWSFE cms rest'
        property "sonar.sources", 'src/main'
        property "sonar.tests", 'src/test'
        property "sonar.sourceEncoding", 'UTF-8'
    }
}
